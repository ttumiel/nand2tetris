// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array powersOf2;
    static int bitWidth;

    /** Initializes the library. */
    function void init() {
        let bitWidth = 16;

        let powersOf2 = Array.new(16);
        let powersOf2[0] = 1;
        let powersOf2[1] = 2;
        let powersOf2[2] = 4;
        let powersOf2[3] = 8;
        let powersOf2[4] = 16;
        let powersOf2[5] = 32;
        let powersOf2[6] = 64;
        let powersOf2[7] = 128;
        let powersOf2[8] = 256;
        let powersOf2[9] = 512;
        let powersOf2[10] = 1024;
        let powersOf2[11] = 2048;
        let powersOf2[12] = 4096;
        let powersOf2[13] = 8192;
        let powersOf2[14] = 16384;
        let powersOf2[15] = 16384+16384;

        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x<0){
            return -x;
        } else {
            return x;
        }
    }

    /** Returns true if the i'th bit of x is 1 */
    function boolean bit(int x, int i) {
        return ~((x & powersOf2[i]) = 0);
    }

    /** Returns the product of x and y.
     *  When a Jack compiler detects the multiplication operator '*' in the
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int sum, count, w;

        let w = bitWidth;
        let sum = 0;
        let count = 0;
        while (count < w){
            if (Math.bit(x, count)){
                let sum = sum + y;
            }
            let y = y + y;
            let count = count + 1;
        }

        return sum;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var int quot;
        var boolean negX, negY;

        let negX = x<0;
        let negY = y<0;
        let x = Math.abs(x);
        let y = Math.abs(y);

        if (y>x){ return 0; }
        let quot = x / (y+y);
        if(x-(2*quot*y) < y){
            let quot = quot+quot;
        } else {
            let quot = quot+quot+1;
        }

        if (negX = negY){
            return quot;
        } else {
            return -quot;
        }
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int i, out, tmpSum, tmpProd;

        let i = 7; // (bitWidth/2) - 1;
        let out = 0;
        while (~(i < 0)){
            let tmpSum = out + powersOf2[i];
            let tmpProd = tmpSum*tmpSum;
            if (~(tmpProd>x) & (tmpProd>0)){
                let out = tmpSum;
            }
            let i = i - 1;
        }
        return out;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a > b){
            return a;
        } else {
            return b;
        }
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b){
            return a;
        } else {
            return b;
        }
    }

    /** Calculates the remainder of a/b */
    function int mod(int a, int b){
        return a - (a/b*b);
    }

    /** Sets the ith bit of x to y */
    function int setBit(int x, int i, boolean y){
        var int val;
        if (y){
            return x | powersOf2[i];
        }
        else {
            return x & (~powersOf2[i]);
        }
    }

}
